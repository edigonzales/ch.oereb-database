import ch.so.agi.gretl.tasks.*
import java.nio.file.Paths
import de.undercouch.gradle.tasks.download.Download

defaultTasks "importData"

def GROUP = "AV-Import"

def dbSchemas = ["live_so"]

def pathToTempFolder = System.getProperty("java.io.tmpdir")
def pathToUnzipFolder = Paths.get(pathToTempFolder, "unzip_data")
def iliModelAmtlicheVermessung = "DM01AVCH24LV95D"

def cadastralSurveyingBaseUrl = "https://files.geo.so.ch/ch.so.agi.av.dm01_ch/aktuell/"
//def cadastralSurveyingDataSets = ["2430", "2457", "2491", "2492", "2501", "2502", "2546", "2572", "2573", "2574", "2580", "2582", "2585", "2601"]
def cadastralSurveyingDataSets = ["2546"]

cadastralSurveyingDataSets.each { cadastralSurveyingDataSet ->
    def dataSet = cadastralSurveyingDataSet.toString()
    task "downloadCadastralSurveyingData_$dataSet"(type: Download) {
        group = GROUP
        description = "Download AV-Datensatz: ${dataSet}"
        src cadastralSurveyingBaseUrl + dataSet + ".ch.so.agi.av.dm01_ch.itf.zip"
        dest pathToTempFolder
        overwrite true

        doLast {
            println "File downloaded to: " + pathToTempFolder
        }        
    }

    task "unzipCadastralSurveyingData_$dataSet"(type: Copy, dependsOn: "downloadCadastralSurveyingData_$dataSet") {
        group = GROUP
        description = "Unzip heruntergeladene Daten"
        from zipTree(Paths.get(pathToTempFolder, dataSet + ".ch.so.agi.av.dm01_ch.itf.zip"))
        into file(pathToUnzipFolder)
        include "**/*.itf"

        doLast {
            println "File unzipped to directory: " + pathToUnzipFolder
        }    
    }

    task "replaceCadastralSurveyingData_$dataSet"(type: Ili2pgReplace, dependsOn: "unzipCadastralSurveyingData_$dataSet") {
        group = GROUP
        description = "Import AV-Datensatz: ${dataSet}"
        database = [dbUriOerebV2, dbUserOerebV2, dbPwdOerebV2]
        models = iliModelAmtlicheVermessung
        dbschema = dbSchemas[0]
        dataFile = file(Paths.get(pathToUnzipFolder.toString(), dataSet + ".ch.so.agi.av.dm01_ch.itf"))
        topics = "DM01AVCH24LV95D.Liegenschaften;DM01AVCH24LV95D.Gemeindegrenzen;DM01AVCH24LV95D.Gebaeudeadressen"
        dataset = dataSet
        disableValidation = true

        doLast {
            println "Data imported into db: " + dbUriOerebV2
        }        
    }
}

task importData() {
    description = "Aggregationstask für das Importieren/Ersetzen sämtlicher AV-Daten."
    group = GROUP
    dependsOn {
        tasks.findAll { task -> task.name.startsWith('replaceCadastralSurveyingData_') }
    }    
}